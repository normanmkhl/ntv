apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

android {
    def versionProps = new Properties()
    def versionPropsFile = file('version.prop')
    def versionPropsCode = 1
    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
        versionPropsCode = (versionProps['VERSION_CODE'] ?: "1").toInteger()
    }
    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleRelease)) {
            versionPropsCode += 1
            versionProps['VERSION_CODE'] = versionPropsCode.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "net.harimurti.tv"
        minSdkVersion 19
        //noinspection OldTargetApi
        targetSdkVersion 29 //downgrade for android 11 to make externalstorage readable
        versionCode versionPropsCode
        versionName "2.4"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${defaultConfig.applicationId}_v${defaultConfig.versionName}_b${defaultConfig.versionCode}.apk"
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.13' //from 3.13 require sdk 21+
    implementation 'com.google.android.exoplayer:exoplayer:2.15.1'
    implementation 'com.google.android.exoplayer:exoplayer-rtsp:2.15.1'
    implementation 'com.google.android.exoplayer:extension-rtmp:2.15.1'
    implementation 'com.google.android.gms:play-services-auth:19.2.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.TutorialsAndroid:FilePicker:v8.0.19'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'com.h6ah4i.android.widget.verticalseekbar:verticalseekbar:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
